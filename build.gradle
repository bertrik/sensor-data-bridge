plugins {
    alias libs.plugins.errorprone
    alias libs.plugins.versions
    alias libs.plugins.dependencycheck
    alias libs.plugins.git.version
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyCheck {
    scanConfigurations = ['runtimeClasspath']
}

subprojects {
    apply plugin: "java"
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.github.ben-manes.versions'

    java {
        targetCompatibility = JavaVersion.VERSION_21
        sourceCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
    }

    // Reject all non stable versions in dependency check
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }

    // keep current time stamps in tar
    tasks.withType(Tar).configureEach {
        preserveFileTimestamps = true
    }

    // fix or errorprone on java 21
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += "-XDaddTypeAnnotationsToSymbol=true"
    }

    test {
        useJUnitPlatform()
    }

    // Standard libraries added to all projects
    dependencies {
        errorprone(libs.errorprone.core)
        compileOnly libs.errorprone.annotations

        implementation libs.slf4j.api

        testImplementation(platform(libs.junit.bom))
        testImplementation libs.junit.jupiter
        testRuntimeOnly libs.junit.platform.launcher

        testImplementation libs.mockito.core
    }

}
